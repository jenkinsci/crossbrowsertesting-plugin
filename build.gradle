plugins {
    id 'org.jenkins-ci.jpi' version '0.40.0-rc.2'
    id 'net.researchgate.release' version '2.6.0'
    id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "1.50"
    id 'java'
    id 'idea'
}

group = 'org.jenkins-ci.plugins'
version = '2.5.19'
description = """This plugin integrates Jenkins users with Selenium Testing on CrossBrowserTesting.com. CrossBrowserTesting.com provides cross browser testing of websites, webpages, and web applications on Windows, Macs, and real iPhones, iPads, and Android Phones and Tablets."""

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}
/*
 * gradle-jpi
 * https://github.com/jenkinsci/gradle-jpi-plugin/blob/master/README.md
*/
jenkinsPlugin {
    coreVersion = '2.29'
    displayName = 'CrossBrowserTesting.com Plugin'
    url = 'https://wiki.jenkins-ci.org/display/JENKINS/CrossBrowserTesting+Plugin'
    gitHubUrl = 'http://github.com/jenkinsci/crossbrowsertesting-plugin'
    repoUrl = 'https://repo.jenkins-ci.org/releases'
    fileExtension = 'hpi'

    developers {
        developer {
            id 'crossbrowsertesting'
            name 'CrossBrowserTesting.com LLC'
            email 'info@crossbrowsertesting.com'
        }
    }
    licenses {
        license {
            name 'MIT License'
            url 'http://opensource.org/licenses/MIT'
        }
    }
}
/*
 * gradle-release
 * https://github.com/researchgate/gradle-release/blob/master/README.md
 */
release {
    failOnSnapshotDependencies = false
    failOnUnversionedFiles = false
    tagTemplate = '$name-$version'
    buildTasks = ['build']
    scmAdapters = [net.researchgate.release.GitAdapter]

    git {
        requireBranch = 'master'
        pushToRemote = 'origin'
        commitVersionFileOnly = false
        signTag = false
    }
}

dependencies {
    compile group: 'org.json', name: 'json', version:'20151123'
    compile group: 'commons-codec', name: 'commons-codec', version:'1.9'
    compile group: 'com.fizzed', name: 'jne', version: '3.0.0'
    compile group: 'com.github.crossbrowsertesting', name: 'ci-common', version:'1.4.12'
    compile group: 'org.jenkins-ci.plugins.workflow', name: 'workflow-step-api', version: '2.13'
    compile group: 'org.jenkins-ci.plugins', name: 'credentials', version: '2.1.16'
   
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.0")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.7.0")
}

test {
    useJUnitPlatform()
}

/*
 * specify a Jenkins version
 * otherwise uses what is specified in 'jenkinsPlugin.coreVersion'
 * -Pjenkins=${jenkins_version}
 */
if (project.hasProperty('jenkins')) {
    jenkinsPlugin.coreVersion = project.getProperty('jenkins')
}

task changelog(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
    file = new File("CHANGELOG.md");
    templateContent = """

{{#tags}}
## {{name}}
{{#issues}}
### {{title}}
{{#commits}}
  {{#hash}}
  {{#message}}
   * [{{hash}}](https://github.com/jenkinsci/crossbrowsertesting-plugin/commit/{{hash}}) {{{message}}}
  {{/message}}
  {{/hash}}
{{/commits}}
{{/issues}}
{{/tags}}
 """;
}
